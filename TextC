Gui, Font, s15
Gui, Add, Text,, % "------------------------------------------------------"
Gui, Show
Gui, +LastFound +HWND_G
 
SetTaskbarProgress( 0, _G )
Sleep 100
Loop, 50 {
	SetTaskbarProgress( A_Index, _G )
	Sleep 50
}
Sleep 100
SetTaskbarProgress( "Paused", _G )
Sleep, 1000
SetTaskbarProgress( "Normal", _G )
Sleep, 1000
Loop, 49 {
	SetTaskbarProgress( A_Index * 2, _G )
	Sleep 100
}
Sleep 100
SetTaskbarProgress( "Error", _G )
Sleep 1000
SetTaskbarProgress( "Indeterminate", _G )
Sleep 1000
ExitApp

;###############################################################
;establecer porcentaje de progreso en la barra de tareas
;Sintaxis: SetTaskbarProgress( [ESTADO], [Ventana ID] )
;ESTADOS:
	;0~100 = porcentaje
	;Normal, Paused, Indeterminate, Error = estado
SetTaskbarProgress(State, hWnd := "") { ;http://ahkscript.org/boards/viewtopic.php?p=48299#p48299
	static ppv
	if !ppv
		DllCall("ole32.dll\OleInitialize", "PtrP", 0)
		, VarSetCapacity(CLSID, 16), VarSetCapacity(riid, 16)
		, DllCall("ole32.dll\CLSIDFromString", "Str", "{56FDF344-FD6D-11d0-958A-006097C9A090}", "Ptr", &CLSID)
		, DllCall("ole32.dll\CLSIDFromString", "Str", "{ea1afb91-9e28-4b86-90e9-9e9f8a5eefaf}", "Ptr", &riid)
		, DllCall("ole32.dll\CoCreateInstance", "Ptr", &CLSID, "Ptr", 0, "UInt", 21, "Ptr", &riid, "PtrP", ppv)
	hWnd := hWnd ? hWnd : IsWindow()
	s0 := 0, sI := sIndeterminate := 1, sN := sNormal := 2, sE := sError := 4, sP := sPaused := 8
	if InVar( State, "0,N,P,E,I,Normal,Paused,Error,Indeterminate" )
		return DllCall(NumGet(NumGet(ppv+0)+10*A_PtrSize), "Ptr", ppv, "Ptr", hWnd, "UInt", s%State%)
	return DllCall(NumGet(NumGet(ppv+0)+9*A_PtrSize), "Ptr", ppv, "Ptr", hWnd, "Int64", State * 10, "Int64", 1000)
}

;If [var] in [ .. ]
InVar(Haystack, Needle, Delimiter := ",", OmitChars := "") {
	Loop, Parse, % Needle, %Delimiter%, %OmitChars%
		if (A_LoopField = Haystack)
			return 1
	return 0
}

IsWindow(hWnd*) {
	if !hWnd.MaxIndex()
		return DllCall("User32.dll\GetForegroundWindow")
	return i := DllCall("User32.dll\IsWindow", "Ptr", hWnd[1] )
		, ErrorLevel := !i
}
